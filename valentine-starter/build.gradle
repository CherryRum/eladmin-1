plugins {
    id 'com.lwohvye.java-conventions'
    id 'org.gradlex.extra-java-module-info'
    id 'org.springframework.boot' // 通过引入这个，使该subModule有了相关的Task
}

dependencies {
    implementation project(':unicorn-security')
    implementation project(':unicorn-logging')
    implementation project(':unicorn-tp-tools')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compileOnlyApi "org.projectlombok:lombok:${lombokVersion}"
    compileOnlyApi "org.mapstruct:mapstruct-processor:$mapstructVersion"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    implementation(project(':unicorn-security')) {
        capabilities {
            requireCapability('com.lwohvye:unicorn-security-captcha')
        }
    }
    implementation(project(':unicorn-security')) {
        capabilities {
            // 这里只支撑横线，不支持驼峰
            requireCapability('com.lwohvye:unicorn-security-business-log')
        }
    }
}

extraJavaModuleInfo {
    automaticModule("io.github.mouzt:bizlog-sdk", 'com.mzt.logapi')
    automaticModule("com.github.whvcse:easy-captcha", 'com.wf.captcha')
    automaticModule("ch.ethz.ganymed:ganymed-ssh2", 'ch.ethz.ssh2')
    automaticModule("cn.hutool:hutool-all", 'cn.hutool')
    automaticModule("io.jsonwebtoken:jjwt-api", 'io.jsonwebtoken.api')
    automaticModule("io.jsonwebtoken:jjwt-impl", 'io.jsonwebtoken.impl')
    automaticModule("io.jsonwebtoken:jjwt-jackson", 'io.jsonwebtoken.jackson')
    automaticModule("com.jcraft:jsch", 'com.jcraft.jsch')
    automaticModule("org.mapstruct.extensions.spring:mapstruct-spring-extensions", 'org.mapstruct.extensions.spring')
    automaticModule("org.quartz-scheduler:quartz", 'org.quartz')
    automaticModule("org.redisson:redisson", 'org.redisson')
    automaticModule("org.springframework.retry:spring-retry", 'spring.retry')
    automaticModule("net.coobird:thumbnailator", 'net.coobird.thumbnailator')
    module('org.eclipse.angus:jakarta.mail', 'jakarta.mail', '1.0.0') {
        uses('jakarta.mail.util.StreamProvider')
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named("bootBuildImage") {
    environment = [
            "BP_JVM_VERSION"              : "19",
            "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
            "BPE_APPEND_JAVA_TOOL_OPTIONS": "-XX:+UseZGC --enable-preview"
    ]
}
