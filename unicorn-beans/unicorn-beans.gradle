description = '基础Bean模块'

java {
    withJavadocJar()
}

jar {
    enabled = true
    manifest {
        from sharedManifest
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Automatic-Module-Name": "lwohvye." + project.name.replace("-", ".")
        )
    }
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaBeansPublication }
        rename ".*", "pom.xml"
    }
}

publishing {
    publications {
        mavenJavaBeans(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Unicorn Beans'
                description = 'Beans & Configuration module'
                url = 'https://github.com/lWoHvYe/unicorn.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lWoHvYe'
                        name = '王红岩(lWoHvYe)'
                        email = 'lWoHvYe@outlook.com'
                        url = 'https://www.lwohvye.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/lWoHvYe/unicorn.git'
                    developerConnection = 'scm:git:ssh://github.com/lWoHvYe/unicorn.git'
                    url = 'https://github.com/lWoHvYe/unicorn/tree/main'
                }
            }
        }
    }
}

dependencies {
    api project(':unicorn-core')
}
